/************************** REXX ************************************/
/*                                                                  */
/* NAME:  GITUTIL                                                   */
/*                                                                  */
/*    DESCRIPTION: GIT UTILITY GENERATOR.                           */
/*                                                                  */
/*                                                                  */
/********************************************************************/
/*                                                                  */
/*                    LOG OF CHANGES                                */
/*                                                                  */
/* VER  CONTROL  YYYY/MM/DD  ANALYST   DESCRIPTION                  */
/* ---  -------  ----------  -------   -----------                  */
/*                                                                  */
/* A00  NONE     2018/09/18  BALAJI    INITIAL ISSUANCE             */
/* A01  NONE     2018/11/09  BALAJI    SSH OPTION AND PIPELINE TEST */
/********************************************************************/
X = MSG("OFF")
       
ADDRESS ISPEXEC "VGET  (ZSYSID)  SHARED"
IF  ZSYSID = 'DEVA' THEN
  DO
    SAY "***************************************"
    SAY "*  TSO GIT IS NOT SUPPORTED ON DEVA   *"
    SAY "*  LOGON TO ANOTHER LPAR ON TSDEV     *"
    SAY "***************************************"
    EXIT
  END
/*GET THE USERID TO USE AS HLQ FOR DATASET ALLOCATION*/
USRID = SUBSTR(USERID(),1,4)
UPPERCS = 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
LOWERCS = 'abcdefghijklmnopqrstuvwxyz'
LOWUSR = TRANSLATE(USRID, LOWERCS, UPPERCS)

PLIB = 'C03058.MFCD.GIT.ISPPLIB' 
SLIB = 'C03058.MFCD.GIT.ISPSLIB' 
TLIB = 'C03058.MFCD.GIT.ISPTLIB' 
ALIB = 'C03058.MFCD.GIT.ISPCLIB' 
ADDRESS ISPEXEC "LIBDEF  ISPPLIB DATASET ID('"PLIB"') STACK"
ADDRESS ISPEXEC "LIBDEF  ISPSLIB DATASET ID('"SLIB"') STACK"
ADDRESS ISPEXEC "LIBDEF  ISPTLIB DATASET ID('"TLIB"') STACK"
"ALTLIB ACTIVATE APPLICATION(CLIST) DATASET('"ALIB"')"
/*SKLTRC = 'DQ9U.SKEL.TRACE'
"ALLOC F(ISPFTTRC) DA('"SKLTRC"') SHR "*/           
/* CHECK IF BEING CALLED FROM JCL FOR PIPELINE TESTING*/
/* IF BEING CALLED FROM TSO DISPLAY THE MAIN PANEL*/

IF ARG() = 0 THEN
  DO
    CALL_FRM_PNL = 'Y'
    ADDRESS ISPEXEC "DISPLAY PANEL(SACXP)"
  END
ELSE
DO
   PARSE UPPER ARG PARM 'GITOPT('GITOPT')'
   PARSE UPPER ARG PARM 'GITGRP('GITGRP')'
   PARSE UPPER ARG PARM 'GITPROJ('GITPROJ')'
   PARSE ARG PARM 'PGMNAME('PGMNAME')'
   PARSE UPPER ARG PARM 'BRNCHFLG('BRNCHFLG')'
   PARSE UPPER ARG PARM 'BRNCHNAM('BRNCHNAM')'
   PARSE UPPER ARG PARM 'PDSHLQ('PDSHLQ')'
   PARSE UPPER ARG PARM 'CBLFLG('CBLFLG')'
   PARSE UPPER ARG PARM 'PLIFLG('PLIFLG')'
   PARSE UPPER ARG PARM 'JCLFLG('JCLFLG')'
   PARSE UPPER ARG PARM 'LNKFLG('LNKFLG')'
   PARSE UPPER ARG PARM 'SUBJOB('SUBJOB')'
   PARSE UPPER ARG PARM 'REXFLG('REXFLG')'
   PARSE UPPER ARG PARM 'SKELFLG('SKELFLG')'
   PARSE UPPER ARG PARM 'PNLFLG('PNLFLG')'
   PARSE UPPER ARG PARM 'MSGFLG('MSGFLG')'
   PARSE UPPER ARG PARM 'CPYFLG('CPYFLG')'
   PARSE UPPER ARG PARM 'PSBFLG('PSBFLG')'
   PARSE UPPER ARG PARM 'DBDFLG('DBDFLG')'
   PARSE UPPER ARG PARM 'BMSFLG('BMSFLG')'
END

/* CHOOSE WHICH SECONDARY PANEL TO DISPLAY */

DO WHILE (RC = 0)
  SELECT
    WHEN(GITOPT = '0') THEN

      CALL SSH_SETUP_PARA

    WHEN(GITOPT = '1') THEN

      CALL GIT_CLONE_PARA

    WHEN(GITOPT = '2') THEN

      CALL GIT_PULL_PARA

    WHEN(GITOPT = '3') THEN

      CALL GIT_PUSH_PARA

    WHEN(GITOPT = '4') THEN

      CALL GIT_ADVANCE_PARA

    OTHERWISE
      NOP
  END
GITOPT = ''
  IF CALL_FRM_PNL = 'Y' THEN
    DO
      ADDRESS ISPEXEC "DISPLAY PANEL(SACXP)"
    END
  IF CALL_FRM_PNL <> 'Y' THEN
    DO
      RC = 8
    END
END

EXIT





SSH_SETUP_PARA :

IF CALL_FRM_PNL = 'Y' THEN
DO
  ADDRESS ISPEXEC "DISPLAY PANEL(SACXT)"
END
DO WHILE (RC = 0)
  SELECT
   WHEN (SSHOPT = '1') THEN
   DO

     ADDRESS ISPEXEC "FTOPEN TEMP"
     ADDRESS ISPEXEC "FTINCL KBB0R"
     ADDRESS ISPEXEC "FTCLOSE"
     ADDRESS ISPEXEC "VGET (ZTEMPF) SHARED"
     JCLPS = ZTEMPF
     GITFNC= SSHGEN
     CALL SUB_JCL_PARA
   END
   WHEN (SSHOPT = '2') THEN
   DO

     ADDRESS ISPEXEC "FTOPEN TEMP"
     ADDRESS ISPEXEC "FTINCL KBB0S"
     ADDRESS ISPEXEC "FTCLOSE"
     ADDRESS ISPEXEC "VGET (ZTEMPF) SHARED"
     JCLPS = ZTEMPF
     GITFNC= SSHTST
     CALL SUB_JCL_PARA
   END
    OTHERWISE
      NOP
  END
SSHOPT = ''
  IF CALL_FRM_PNL = 'Y' THEN
    DO
     ADDRESS ISPEXEC "DISPLAY PANEL(SACXT)"
    END
  IF CALL_FRM_PNL <> 'Y' THEN
    DO
      RC = 8
    END
END

RETURN

GIT_CLONE_PARA :

IF CALL_FRM_PNL = 'Y' THEN
  DO
    ADDRESS ISPEXEC "DISPLAY PANEL(SACXQ)"
  END
CALL CHK_FLAG_PARA
/*ISPFTTRC*/                        
ADDRESS ISPEXEC "FTOPEN TEMP"
ADDRESS ISPEXEC "FTINCL KBB0H"
ADDRESS ISPEXEC "FTCLOSE"
ADDRESS ISPEXEC "VGET (ZTEMPF) SHARED"
JCLPS = ZTEMPF
GITFNC= CLONE
CALL SUB_JCL_PARA

RETURN

GIT_PULL_PARA :

IF CALL_FRM_PNL = 'Y' THEN
  DO
    ADDRESS ISPEXEC "DISPLAY PANEL(SACXR)"
  END
CALL CHK_FLAG_PARA
ADDRESS ISPEXEC "FTOPEN TEMP"
ADDRESS ISPEXEC "FTINCL KBB0J"
ADDRESS ISPEXEC "FTCLOSE"
ADDRESS ISPEXEC "VGET (ZTEMPF) SHARED"
JCLPS = ZTEMPF
GITFNC=PULL
CALL SUB_JCL_PARA

RETURN

GIT_PUSH_PARA :

IF CALL_FRM_PNL = 'Y' THEN
  DO
    ADDRESS ISPEXEC "DISPLAY PANEL(SACXS)"
  END
CALL CHK_FLAG_PARA
ADDRESS ISPEXEC "FTOPEN TEMP"
ADDRESS ISPEXEC "FTINCL KBB0K"
ADDRESS ISPEXEC "FTCLOSE"
ADDRESS ISPEXEC "VGET (ZTEMPF) SHARED"
JCLPS = ZTEMPF
GITFNC=PUSH
CALL SUB_JCL_PARA

RETURN

GIT_ADVANCE_PARA :

ADDRESS ISPEXEC "FTOPEN TEMP"
ADDRESS ISPEXEC "FTINCL KBB0L"
ADDRESS ISPEXEC "VGET (ZTEMPF) SHARED"
JCLPS = ZTEMPF
ADDRESS ISPEXEC "FTCLOSE"
IF CALL_FRM_PNL = 'Y' THEN
  DO
    ADDRESS ISPEXEC "EDIT DATASET('"JCLPS"')"
  END
ADDRESS TSO "FREE DA('"JCLPS"') "
CALL C0CZJ 'USRID('USRID')' 'GITFNC('ADVANCE')'
RETURN

SUB_JCL_PARA :
IF CALL_FRM_PNL = 'Y' THEN
DO
  ADDRESS ISPEXEC "DISPLAY PANEL(SACYB)"
END
LOG.=''
STAT = MSG("ON")
IF SUBJOB = 1 THEN DO
  CALL C0CZJ 'USRID('USRID')' 'GITFNC('GITFNC')'
  X = OUTTRAP("LOG.")
  ADDRESS TSO
  "SUB '"JCLPS"' "
  X = OUTTRAP("OFF")
  PARSE VAR LOG.1 "JOB" JBNAME "("JBID")" STATUS
  SAY JBNAME JBID "SUBMITTED"
END
IF CALL_FRM_PNL = 'Y' THEN    
DO
  IF SUBJOB = 2 THEN DO
    CALL C0CZJ 'USRID('USRID')' 'GITFNC('GITFNC')'
    ADDRESS ISPEXEC "EDIT DATASET('"JCLPS"')"
  END
END
ADDRESS TSO "FREE DA('"JCLPS"') "

RETURN

CHK_FLAG_PARA :

  UPPER CBLFLG
  UPPER PLIFLG
  UPPER LNKFLG
  UPPER JCLFLG
  UPPER REXFLG
  UPPER PNLFLG
  UPPER MSGFLG
  UPPER SKELFLG
  UPPER CPYFLG
  UPPER PSBFLG
  UPPER DBDFLG
  UPPER BMSFLG

  IF CBLFLG = 'Y'  THEN
    DO
      LISTDS = PDSHLQ".COBOL.LIST"
      TBLNAM = "CBLLIST"
      COMPTYP = "COBOL"
      CALL READ_LIST_PARA
    END
  IF PLIFLG = 'Y'  THEN
    DO
      LISTDS = PDSHLQ".PLI.LIST"
      TBLNAM = "PLILIST"
      COMPTYP = "PLI"
      CALL READ_LIST_PARA
    END
  IF JCLFLG = 'Y'  THEN
    DO
      LISTDS = PDSHLQ".JCL.LIST"
      TBLNAM = "JCLLIST"
      COMPTYP = "JCL"
      CALL READ_LIST_PARA
    END
  IF LNKFLG = 'Y'  THEN
    DO
      LISTDS = PDSHLQ".LNK.LIST"
      TBLNAM = "LNKLIST"
      COMPTYP = "LNK"
      CALL READ_LIST_PARA
    END
  IF REXFLG = 'Y'  THEN
    DO
      LISTDS = PDSHLQ".REX.LIST"
      TBLNAM = "REXLIST"
      COMPTYP = "REXX"
      CALL READ_LIST_PARA
    END
  IF PNLFLG = 'Y'  THEN
    DO
      LISTDS = PDSHLQ".PNL.LIST"
      TBLNAM = "PNLLIST"
      COMPTYP = "PANEL"
      CALL READ_LIST_PARA
    END
  IF MSGFLG = 'Y'  THEN
    DO
      LISTDS = PDSHLQ".MSG.LIST"
      TBLNAM = "MSGLIST"
      COMPTYP = "MESAG"
      CALL READ_LIST_PARA
    END
  IF SKELFLG = 'Y'  THEN
    DO
      LISTDS = PDSHLQ".SKEL.LIST"
      TBLNAM = "SKELLIST"
      COMPTYP = "SKEL"
      CALL READ_LIST_PARA
    END
  IF CPYFLG = 'Y'  THEN
    DO
      LISTDS = PDSHLQ".CPY.LIST"
      TBLNAM = "CPYLIST"
      COMPTYP = "COPYBOOK"
      CALL READ_LIST_PARA
    END
  IF PSBFLG = 'Y'  THEN
    DO
      LISTDS = PDSHLQ".PSB.LIST"
      TBLNAM = "PSBLIST"
      COMPTYP = "PSB"
      CALL READ_LIST_PARA
    END
  IF DBDFLG = 'Y'  THEN
    DO
      LISTDS = PDSHLQ".DBD.LIST"
      TBLNAM = "DBDLIST"
      COMPTYP = "DBD"
      CALL READ_LIST_PARA
    END
  IF BMSFLG = 'Y'  THEN
    DO
      LISTDS = PDSHLQ".BMS.LIST"
      TBLNAM = "BMSLIST"
      COMPTYP = "BMS"
      CALL READ_LIST_PARA
    END
RETURN

READ_LIST_PARA :

IF SYSDSN(''''LISTDS'''') <> 'OK' THEN
  DO
    "ALLOC F(INDD) DA('"LISTDS"') NEW SP(5,5) TR " ,
    "RECFM(F B) LRECL(80) BLKSIZE(8000) RETPD(300) "
    ADDRESS ISPEXEC "EDIT DATASET('"LISTDS"') MACRO(C0CZC) PARM",
    "(COMPTYP)"
  END
    
     
IF CALL_FRM_PNL = 'Y' THEN
  DO
    ADDRESS ISPEXEC "EDIT DATASET('"LISTDS"') "            
  END
"ALLOC F(INDD) DA('"LISTDS"') SHR"
"EXECIO * DISKR INDD(FINIS STEM TEMP."
"FREE F(INDD)"

ADDRESS ISPEXEC "TBCREATE "TBLNAM" KEYS(COMPNAM) NOWRITE REPLACE"

I=4
DO WHILE I <= TEMP.0
  COMPNAM = SUBSTR(TEMP.I,1,5)
  ADDRESS ISPEXEC "TBADD "TBLNAM""
  I = I + 1
END

RETURN
